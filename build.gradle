/*
 * Copyright (c) 2017. Jan Wiemer
 */
 
plugins {
    id 'java'
    id 'maven-publish'
}

group = "org.jacis"
version = "1.58.0"
version = Boolean.getBoolean("releasebuild") ? "$version" : "$version-SNAPSHOT"


ext {
    projectName            = "jacis"
    projectDescription     = "Java ACS Store - Transient and transactional store for Java objects."
    projectURL             = "https://github.com/JanWiemer/jacis"
    author                 = "Jan Wiemer"
    authorId               = "JanWiemer"
    authorMail             = "jan.wiemer@online.de"
    licenseName            = "Apache-2.0 License"
    licenseURL             = "https://github.com/JanWiemer/jacis/blob/master/LICENSE"
    isReleaseVersion       = !version.endsWith("SNAPSHOT")
    deploy_user            = System.properties.get("deploy_user")
    deploy_pw              = System.properties.get("deploy_pw")
    deploy_repo_release    = System.properties.get("deploy_repo_release")
    deploy_repo_snapshot   = System.properties.get("deploy_repo_snapshot")
}

print '---------- JACIS BUILD -------- '
println new Date()
println "- projectName = $projectName"
println "- description = $projectDescription"
println "- URL         = $projectURL"
println "- group       = $group"
println "- version     = $version"
println "- author      = $author"
println "- authorMail  = $authorMail"
println "- licenseName = $licenseName"
println "- licenseURL  = $licenseURL"
println "- isReleaseV. = $isReleaseVersion"
println "- deploy_user = $deploy_user"
println "- deploy_repo_release  = $deploy_repo_release"
println "- deploy_repo_snapshot = $deploy_repo_snapshot"
println "- root directory = ${rootDir}"
println '------------------------------- '

dependencies {
    // logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    // transaction
    implementation group: 'javax.transaction', name: 'jta', version: '1.1'
    // microstream persistence
    implementation group: 'one.microstream', name: 'storage.embedded', version: '04.00.00-MS-GA'
    implementation group: 'one.microstream', name: 'storage.embedded.configuration', version: '04.00.00-MS-GA'
    implementation group: 'one.microstream', name: 'persistence.binary.jdk8', version: '04.00.00-MS-GA'
    // test
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

clean.doFirst {
    delete "${rootDir}/var/"
    delete "${rootDir}/log/"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.microstream.one/repository/maven-public' }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    exclude 'logback.xml'
}

jar.doFirst {
    manifest {
        attributes 'Implementation-Title'     : projectName,
                   'Implementation-Version'   : archiveVersion,
                   'Specification-Title'      : new Date(),
                   'Implementation-Vendor'    : author
    }
}

publishing {
  publications {
    jacisPublication(MavenPublication) {
      from components.java
      pom { 
        name = projectName
        description = projectDescription
        url = projectURL
        licenses {
          license {
            name = licenseName
            url = licenseURL 
          }
        }
        developers {
          developer {
            id = authorId
            name = author
            email = authorMail
          }
        }
      } 
    } 
  }
  repositories {
    maven {
      credentials {
        username deploy_user
        password deploy_pw
      }
      if (isReleaseVersion) {
        url deploy_repo_release
      } else {
        url deploy_repo_snapshot
      }
    }
  }  
}
