/*
 * Copyright (c) 2017. Jan Wiemer
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'org.jacis'
version = '1.6'
version = Boolean.getBoolean("releasebuild") ? "$version" : "$version-SNAPSHOT"

ext {
    vendor                 = 'Jan Wiemer'
    projectName            = 'jacis'
    projectDescriotion     = 'Java ACS Store - Transient and transactional store for Java objects,'
    projectURL             = 'https://github.com/JanWiemer/jacis'
    isReleaseVersion       = !version.endsWith("SNAPSHOT")
    deploy_user            = System.properties.get("deploy_user")
    deploy_pw              = System.properties.get("deploy_pw")
    deploy_repo_release    = System.properties.get("deploy_repo_release")
    deploy_repo_snapshot   = System.properties.get("deploy_repo_snapshot")
}

print '---------- JACIS BUILD -------- '
println new Date()
println "- projectName = $projectName"
println "- descriotion = $projectDescriotion"
println "- URL         = $projectURL"
println "- group       = $group"
println "- version     = $version"
println "- vendor      = $vendor"
println "- deploy_repo_release  = $deploy_repo_release"
println "- deploy_repo_snapshot = $deploy_repo_snapshot"
println '------------------------------- '

configurations {
    asciidoclet
}

jar {
    exclude 'logback.xml'
}

jar.doFirst {
    manifest {
        attributes 'Implementation-Title'     : projectName,
                   'Implementation-Version'   : version,
                   'Specification-Title'      : new Date(),
                   'Implementation-Vendor'    : vendor
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "$group"
            artifactId "$projectName"
            version "$version"
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name "$projectName"
                    description  "$projectDescriotion"
                    licenses {
                        license {
                            name 'GNU GENERAL PUBLIC LICENSE, Version 2'
                            url 'https://github.com/JanWiemer/jacis/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id 'JanWiemer'
                            name 'Jan Wiemer'
                            email 'jan.wiemer@online.de'
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username deploy_user
                password deploy_pw
            }
            if (isReleaseVersion) {
                url deploy_repo_release
            } else {
                url deploy_repo_snapshot
            }
        }
    }
}

artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file:///"+getProject().getBuildDir()+"/../dist/jacisreleases/maven-repository/")
        pom.project {
          licenses {
            license {
                name 'GNU GENERAL PUBLIC LICENSE, Version 2'
                url 'https://github.com/JanWiemer/jacis/blob/master/LICENSE'
            }
          }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    runtime group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    runtime group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.7'
    runtime group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.2'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'javax.transaction', name: 'jta', version: '1.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    asciidoclet 'org.asciidoctor:asciidoclet:1.+'
}

javadoc {
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.overview = "src/main/java/overview.adoc"
    options.addStringOption "-base-dir", "${projectDir}"
    options.addStringOption "-attribute",
            "name=${project.name}," +
            "version=${project.version}"
}
