/*
 * Copyright (c) 2017. Jan Wiemer
 */
 
plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.jacis'
version = '1.54'
version = Boolean.getBoolean("releasebuild") ? "$version" : "$version-SNAPSHOT"


ext {
    vendor                 = 'Jan Wiemer'
    projectName            = 'jacis'
    projectDescription     = 'Java ACS Store - Transient and transactional store for Java objects,'
    projectURL             = 'https://github.com/JanWiemer/jacis'
    isReleaseVersion       = !version.endsWith("SNAPSHOT")
    deploy_user            = System.properties.get("deploy_user")
    deploy_pw              = System.properties.get("deploy_pw")
    deploy_repo_release    = System.properties.get("deploy_repo_release")
    deploy_repo_snapshot   = System.properties.get("deploy_repo_snapshot")
}

print '---------- JACIS BUILD -------- '
println new Date()
println "- projectName = $projectName"
println "- description = $projectDescription"
println "- URL         = $projectURL"
println "- group       = $group"
println "- version     = $version"
println "- vendor      = $vendor"
println "- deploy_repo_release  = $deploy_repo_release"
println "- deploy_repo_snapshot = $deploy_repo_snapshot"
println '------------------------------- '

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'javax.transaction', name: 'jta', version: '1.1'
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

//java {
//    withJavadocJar()
//    withSourcesJar()
//}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier "javadoc"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    exclude 'logback.xml'
}

jar.doFirst {
    manifest {
        attributes 'Implementation-Title'     : projectName,
                   'Implementation-Version'   : archiveVersion,
                   'Specification-Title'      : new Date(),
                   'Implementation-Vendor'    : vendor
    }
}

publishing {
  publications {
    jacisPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = "$projectName"
        description = "$projectDescription"
        url = "$projectURL"
        licenses {
          license {
            name = "GNU LESSER GENERAL PUBLIC LICENSE, Version 3"
            url = "https://github.com/JanWiemer/jacis/blob/master/LICENSE"
          }
        }
        developers {
          developer {
            id = "JanWiemer"
            name = "Jan Wiemer"
            email = "jan.wiemer@online.de"
          }
        }
      }
    }
  }
  repositories {
    maven {
      credentials {
        username deploy_user
        password deploy_pw
      }
      if (isReleaseVersion) {
        url deploy_repo_release
      } else {
        url deploy_repo_snapshot
      }
    }
  }  
}
